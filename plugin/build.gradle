apply plugin: "com.github.gmazzo.buildconfig"

ScreamingLibBuilder()
        .platforms('bukkit')
        .modules('core', 'command', 'tasker', 'hologram', 'placeholders', 'sidebar', 'healthindicator', 'npc')
        .version(SCREAMING_LIB_VERSION)
        .simpleInventories(SIMPLE_INVENTORIES_VERSION)
        .universalModules('signs', 'lang')
        .enableAnnotationProcessor()
        .build()

dependencies {
    /* PROVIDED */
    compileOnly paperLegacy(PAPER_VERSION)
    compileOnly vault(VAULT_VERSION)
    compileOnly 'com.onarandombox.multiversecore:Multiverse-Core:' + MULTIVERSE_VERSION
    compileOnly('me.ebonjaeger:perworldinventory-kt:' + PERWORLDINVENTORY_KT_VERSION)  {
        exclude group: '*', module: '*'
    }
    compileOnly('me.gnat008:perworldinventory:' + PERWORLDINVENTORY_VERSION) {
        exclude group: '*', module: '*'
    }
    compileOnly 'com.alessiodp.parties:parties-api:' + PARTIES_VERSION

    /* SHADED */
    implementation project(':BedWars-API')
    implementation project(':BedWars-NMS')
    implementation 'org.screamingsandals.language.bedwars:BedWarsLanguage:' + (BEDWARS_VERSION =~ /^\d+.\d+/).findAll()?.first() + '-SNAPSHOT'
    implementation 'com.zaxxer:HikariCP:3.3.0'
    implementation 'cloud.commandframework:cloud-minecraft-extras:' + CLOUD_EXTRAS_VERSION, {
        exclude group: 'net.kyori'
    }
    implementation bStats(BSTATS_VERSION)
    implementation 'me.lucko:commodore:' + COMMODORE_VERSION, {
        exclude group: 'com.mojang', module: 'brigadier'
    }
}

buildConfig {
    className('VersionInfo')
    packageName('org.screamingsandals.bedwars')

    buildConfigField('String', 'NAME', "\"${project.name}\"")
    buildConfigField('String', 'VERSION', "\"${project.version}\"")
    buildConfigField('String', 'BUILD_NUMBER', "\"${System.getenv('BUILD_NUMBER') ?: "custom"}\"")
}

prepareTestTask()
    .versions('1.17.1', '1.16.5', '1.15.2', '1.14.4', '1.13.2', '1.12.2', '1.11.2', '1.10.2', '1.9.4')
    .setSubdirectory('paper-master')
    .jvmArgs('-Dio.papermc.paper.suppress.sout.nags=true', '-DPaper.IgnoreJavaVersion=true') // suppress System.out.println nag
    .onlineMode(false)
    .pluginJar(shadowJar.archiveFile.get().asFile.toPath())
    .build()