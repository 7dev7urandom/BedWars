apply plugin: 'de.fuerstenau.buildconfig'

ScreamingLibBuilder()
        .platforms('bukkit')
        .modules('player', 'event', 'material', 'entity', 'world', 'command', 'tasker', 'hologram')
        .version(SCREAMING_LIB_VERSION)
        .simpleInventories(SIMPLE_INVENTORIES_VERSION)
        .universalModules('signs', 'lang')
        .enableAnnotationProcessor()
        .build()

dependencies {
    /* PROVIDED */
    compileOnly vault(VAULT_VERSION)
    compileOnly 'com.onarandombox.multiversecore:Multiverse-Core:' + MULTIVERSE_VERSION
    compileOnly placeholderapi(PLACEHOLDERAPI_VERSION)
    compileOnly('me.ebonjaeger:perworldinventory-kt:' + PERWORLDINVENTORY_KT_VERSION)  {
        exclude group: '*', module: '*'
    }
    compileOnly('me.gnat008:perworldinventory:' + PERWORLDINVENTORY_VERSION) {
        exclude group: '*', module: '*'
    }
    compileOnly 'io.netty:netty-all:' + NETTY_VERSION
    compileOnly 'net.citizensnpcs:citizens-main:' + CITIZENS_VERSION
    compileOnly 'com.alessiodp.parties:parties-api:' + PARTIES_VERSION

    /* SHADED */
    implementation project(':BedWars-API')
    implementation 'org.screamingsandals.language.bedwars:BedWarsLanguage:' + (BEDWARS_VERSION =~ /^\d+.\d+/).findAll()?.first() + '-SNAPSHOT'
    implementation 'com.zaxxer:HikariCP:3.3.0'
    implementation 'cloud.commandframework:cloud-minecraft-extras:' + CLOUD_EXTRAS_VERSION
    implementation bStats(BSTATS_VERSION)
    implementation 'pronze.lib:scoreboard:1.0.1-SNAPSHOT'
    implementation 'me.lucko:commodore:' + COMMODORE_VERSION, {
        exclude group: 'com.mojang', module: 'brigadier'
    }
}

buildConfig {
    version = project.version
    buildConfigField 'String', 'BUILD_NUMBER', {
        if (System.getenv('BUILD_NUMBER') != null) {
            return System.getenv('BUILD_NUMBER')
        } else {
            return "custom"
        }
    }

    clsName = 'VersionInfo'
    packageName = 'org.screamingsandals.bedwars'
    charset = 'UTF-8'
}