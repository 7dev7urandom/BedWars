defaultTasks 'clean', ':BedWars-NMS:generateNmsComponents', 'screamCompile', ':BedWars-API:allowJavadocUpload'

allprojects {
    group = 'org.screamingsandals.bedwars'
    version = BEDWARS_VERSION
}

buildscript {
    repositories {
        //mavenLocal()
        maven { url 'https://repo.screamingsandals.org/public/' }
        maven { url 'https://plugins.gradle.org/m2' }
    }
    dependencies {
		classpath 'org.screamingsandals.gradle.builder:screaming-plugin-builder:' + SCREAMING_PLUGIN_BUILDER_VERSION
        classpath "com.github.gmazzo:gradle-buildconfig-plugin:" + BUILDCONFIG_VERSION
        classpath 'org.screamingsandals.nms:NMSMapper:' + NMS_MAPPER_VERSION
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.screamingsandals.gradle.builder'

    enableShadowPlugin()

    configurations.all {
        // Check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    repositories {
        //mavenLocal()
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'  }
        maven { url 'https://repo.onarandombox.com/content/groups/public'  }
        maven { url 'https://ci.frostcast.net/plugin/repository/everything'  }
        maven { url 'https://repo.codemc.org/repository/maven-public/'  }
        maven { url 'https://repo.alessiodp.com/releases/'  }
        maven { url 'https://jitpack.io' }
    }
    
    dependencies {
    	compileOnly paperLegacy(PAPER_VERSION)
    }

    shadowJar {
        relocate 'com.zaxxer', 'org.screamingsandals.bedwars.lib.HikariCP'
        relocate 'org.screamingsandals.simpleinventories', 'org.screamingsandals.bedwars.lib.sgui'
        relocate 'org.screamingsandals.lib.utils.Wrapper', 'org.screamingsandals.bedwars.api.utils.Wrapper'
        relocate 'org.screamingsandals.lib', 'org.screamingsandals.bedwars.lib'
        relocate 'org.bstats', 'org.screamingsandals.bedwars.lib.ext.bstats'
        relocate 'io.papermc.lib', 'org.screamingsandals.bedwars.lib.ext.paperlib'
        relocate 'org.spongepowered.configurate', 'org.screamingsandals.bedwars.lib.ext.configurate'
        relocate 'org.yaml.snakeyaml', 'org.screamingsandals.bedwars.lib.ext.snakeyaml'
        relocate 'com.google.gson', 'org.screamingsandals.bedwars.lib.ext.gson'
        relocate 'com.typesafe.config', 'org.screamingsandals.bedwars.lib.ext.typesafe'
        relocate 'io.leangen.geantyref', 'org.screamingsandals.bedwars.lib.ext.geantyref'
        relocate 'net.kyori', 'org.screamingsandals.bedwars.lib.ext.kyori'
        relocate 'org.checkerframework', 'org.screamingsandals.bedwars.lib.ext.checkerframework'
        relocate 'cloud.commandframework', 'org.screamingsandals.bedwars.lib.ext.cloud'
        relocate 'me.lucko.commodore', 'org.screamingsandals.bedwars.lib.ext.commodore'

        if (System.getenv('BUILD_NUMBER') != null) {
            archiveClassifier.set(System.getenv('BUILD_NUMBER'))
        } else {
            archiveClassifier.set('dev')
        }
    }

    sourceCompatibility = '11.0'
}
